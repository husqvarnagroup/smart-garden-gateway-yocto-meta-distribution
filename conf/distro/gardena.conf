DISTRO = "gardena"
DISTRO_NAME = "GARDENA smart Gateway"
DISTRO_VERSION ?= "3"
DISTRO_VERSION_ID ?= "${DISTRO_VERSION}-snapshot-${DATE}"
DISTRO_CODENAME ?= "The Grand Unification"
DISTRO_UPDATE_URL ?= "http://sg-low-cost-gateway-dev.s3.amazonaws.com/images/head/gardena-update-image-prod-${MACHINE}.swu"
SDK_VENDOR = "-gardenasdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "Open Source <smart.open.source@husqvarnagroup.com>"

TARGET_VENDOR = "-gardena"

LOCALCONF_VERSION = "1"

DISTRO_VERSION[vardepsexclude] = "DATE"
SDK_VERSION[vardepsexclude] = "DATE"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_LIBC} systemd wifi ppp ipv6 ipv4"
DISTRO_FEATURES_remove = "pulseaudio"
# remove bluez5 as it is not currently needed
# enable again, if bluetooth will be used
DISTRO_FEATURES_remove = "bluez5"
DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"

PREFERRED_VERSION_swupdate ?= "2019.04"

PREFERRED_VERSION_u-boot = "2019.10-gardena-2"
PREFERRED_VERSION_u-boot_mt7688 = "2019.10-gardena-2-prebuilt"
PREFERRED_VERSION_u-boot-fw-utils = "2019.10"
PREFERRED_VERSION_u-boot-mkimage = "2019.10"

PREFERRED_PROVIDER_u-boot-fw-utils = "u-boot-fw-utils"

TCLIBC ?= "glibc"

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDK_ARCH}-${IMAGE_BASENAME}-${TUNE_PKGARCH}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

GARDENAQEMUDEPS = "${@bb.utils.contains("INCOMPATIBLE_LICENSE", "GPL-3.0", "", "packagegroup-core-device-devel",d)}"
DISTRO_EXTRA_RDEPENDS_append_qemuarm = " ${GARDENAQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemuarm64 = " ${GARDENAQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemumips = " ${GARDENAQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemuppc = " ${GARDENAQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemux86 = " ${GARDENAQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemux86-64 = " ${GARDENAQEMUDEPS}"

TCLIBCAPPEND = ""

QEMU_TARGETS ?= "arm mips mipsel x86_64"

# TODO: Adjust this for GPL compliance. We have to use our own servers.
PREMIRRORS ??= "\
bzr://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
cvs://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
git://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
gitsm://.*/.* http://downloads.yoctoproject.org/mirror/sources/ \n \
hg://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
osc://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
p4://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
svn://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n"

MIRRORS =+ "\
git://stash.dss.husqvarnagroup.com/scm/sg/ git://github.com/husqvarnagroup/ \n \
ftp://.*/.*      http://downloads.yoctoproject.org/mirror/sources/ \n \
http://.*/.*     http://downloads.yoctoproject.org/mirror/sources/ \n \
https://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n"

# The CONNECTIVITY_CHECK_URI's are used to test whether we can succesfully
# fetch from the network (and warn you if not). To disable the test set
# the variable to be empty.
# Git example url: git://git.yoctoproject.org/yocto-firewall-test;protocol=git;rev=master
CONNECTIVITY_CHECK_URIS ?= "https://www.example.com/"

SANITY_TESTED_DISTROS ?= " \
            debian-9 \n \
            debian-10 \n \
            ubuntu-16.04 \n \
            gentoo \n \
            "
#
# OELAYOUT_ABI allows us to notify users when the format of TMPDIR changes in
# an incompatible way. Such changes should usually be detailed in the commit
# that breaks the format and have been previously discussed on the mailing list
# with general agreement from the core team.
#
OELAYOUT_ABI = "12"

# QA check settings - a little stricter than the OE-Core defaults
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths staticdev \
                    useless-rpaths"
WARN_QA_remove = "${WARN_TO_ERROR_QA}"
ERROR_QA_append = " ${WARN_TO_ERROR_QA}"

require conf/distro/include/no-static-libs.inc

# OPKG feeds (development only!)
PACKAGE_FEED_URIS = "http://gateway-dev.iot.sg.dss.husqvarnagroup.net/continuous/master/${MACHINE}"
PACKAGE_FEED_BASE_PATHS = "ipk"
PACKAGE_FEED_ARCHS_mt7688 = "all mips32r2el-24kc-nf gardena_sg_mt7688"
PACKAGE_FEED_ARCHS_at91sam9x5 = "all armv5e gardena_sg_at91sam"

# Copy our U-Boot environment file to the image directory
UBOOT_ENV="uEnv"

# Limit kernel size to 4 MB, which is the maximum our kernel volumes can store
KERNEL_IMAGE_MAXSIZE = "4096"

# This ensures that we always have a somewhat sensible hostname, even before
# changing it to "GARDENA-first-4-chars-of-gatewayid".
hostname_pn-base-files = "GARDENA"

# Store build information on target filesystem on /etc/build
INHERIT += "image-buildinfo"
IMAGE_BUILDINFO_VARS = "DISTRO DISTRO_VERSION DISTRO_VERSION_ID"

# Set a default timezone which is valid for most GARDENA customers
DEFAULT_TIMEZONE = "Europe/Zurich"

# Put Shadoway dependencies in SDK
TOOLCHAIN_TARGET_TASK_append = " lua-staticdev googletest-staticdev linux-libc-headers-dev"
